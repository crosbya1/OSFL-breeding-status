     
# note: the order of breeding states is: FY, P, S

model{
  ### Likelihood
  for (i in 1:N){

    breedingstatus[i] ~ dcat(p[i,1:3]) 
    for (j in 1:3){
      p[i,j] <- delta[i,j]/sum(delta[i,])
      log(delta[i,j]) = alpha[i,j]
    }	
    alpha[i,1] <- eta[1] + eta[4]*JDate[i]
    alpha[i,2] <- eta[2]
    alpha[i,3] <- eta[3] + eta[5]*JDate[i]
	
	  for(j in 1:nsamp[i]){
	    y[i, j] ~ dpois(lambda[i, j]) 
	    log(lambda[i, j]) <- beta0[breedingstatus[i]] + beta1*TimeRel2Sun[i, j]
	    }
    }
  
  # Priors
  # etas are the log odds of each state (with paired as the reference here)
  eta[1] ~ dnorm(0, 0.01)
  eta[2] ~ dnorm(0, 1000)
  eta[3] ~ dnorm(0, 0.01)
  eta[4] ~ dnorm(0, 0.01)
  eta[5] ~ dnorm(0, 0.01)
  
  # exp(beta0) is the expected song count (for each state)
  beta0[1] ~ dnorm(0, 0.01)
  beta0[2] ~ dnorm(0, 0.01)
  beta0[3] ~ dnorm(0, 0.01) 

  beta1 ~ dnorm(0, 0.01)
}


